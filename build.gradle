plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.

	maven {
		url 'https://repo.sleeping.town' //lib39 and featureful
		content {
			includeGroup 'com.unascribed'
			includeGroup 'gay.lemmaeof'
		}
	}
	maven {
		url 'https://api.modrinth.com/maven' //better combat
		content {
			includeGroup 'maven.modrinth'
		}
	}
	maven {
		url 'https://maven.kosmx.dev/' //playeranimator for better combat (*sigh*)
		content {
			includeGroup 'dev.kosmx.player-anim'
		}
	}
	maven {
		url "https://maven.shedaniel.me" //clothconfig for better combat (this is legally considered violence)
		content {
			includeGroup 'me.shedaniel.cloth'
		}
	}
}

loom {
	mods {
		"permet" {
			sourceSet sourceSets.main
		}
	}

}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation include("com.unascribed:lib39-core:${project.lib39_version}+${project.minecraft_version}")
	modImplementation include("com.unascribed:lib39-lockpick:${project.lib39_version}+${project.minecraft_version}")
	modImplementation include("com.unascribed:lib39-machination:${project.lib39_version}+${project.minecraft_version}")
	modImplementation include("gay.lemmaeof:Featureful:${project.featureful_version}")
	modImplementation "maven.modrinth:better-combat:${project.bettercombat_version}+${project.minecraft_version}-fabric"

	//bettercombat dependencies because bettercombat doesn't have a real maven
	modRuntimeOnly 'dev.kosmx.player-anim:player-animation-lib-fabric:1.0.2+1.19.4'
	//this is committing violence against me personally
	modRuntimeOnly 'me.shedaniel.cloth:cloth-config-fabric:11.1.106'
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}